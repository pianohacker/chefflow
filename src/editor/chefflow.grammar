@top Recipe { gridMarker GridLine* | Step* }

@tokens {
	space { @whitespace+ }
	amount { @digit+ | @digit+ "/" @digit+ | "." @digit+ | @digit+ "." @digit+ }
	gridQuantity { "(" @digit+ ")" }
	gridMarker { "Grid:" | "NHA:" | "Manual:" }
}

@external tokens gridMultiWordName from "./tokens" {
	gridMultiWordName
}

@external tokens multiWordName from "./tokens" {
	multiWordName,
	unit
}

@skip { space }

Step { StepDesc ResultName? ":" Ingredient ("," Ingredient)* }
StepDesc { multiWordName }
ResultName { "@" multiWordName }
LastResult { "^" | "above" }
BackReference { ResultName | LastResult }
Amount { amount }
Unit { unit }
IngredientType { multiWordName }
Ingredient { Amount? Unit? IngredientType | BackReference }

GridLine { GridDivider | GridStepDesc GridQuantity | Ingredient }
GridDivider { "--" }
GridStepDesc { gridMultiWordName }
GridQuantity { gridQuantity }
